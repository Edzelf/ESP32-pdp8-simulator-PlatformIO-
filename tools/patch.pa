/PATCH.PA
/Apply patches for ESP32 pdp8 simulator.
/Ed Smallenburg, 16-JAN-2018
/
START,	CLA CLL
	JMS I (DPATCH
	CCL
	JMS I (DPATCH
	DIRECT
	JMP I (7605

/TABLE WITH PATCH INFO.  BLOCK OFFSET, OFFSET IN BLOCK, NEW VALUE
DIRECT,	FILENAME DIRECT.SV
	6 ; 172 ; 0	/ DO NOT ADD 70.
	-1
CCL,	FILENAME CCL.SV
	16 ; 303 ; 5330	/PATCH DAY OF WEEKS FOR CALENDER
	16 ; 304 ; 5335
	16 ; 305 ; 5344
	16 ; 306 ; 5352
	16 ; 307 ; 5312
	16 ; 310 ; 5320
	16 ; 311 ; 5324
	16 ; 171 ; 0000
	16 ; 301 ; 6260	/CENTURY 20
	-1
	PAGE

DPATCH,	HLT
	TAD I DPATCH	/GET ADDRESS OF PARAMETER BLOCK
	ISZ DPATCH
	DCA TMP1
	TAD TMP1	/GET ADDRESS OF FILENAME
	DCA FNB
	CLA CLL IAC RTL	/4
	TAD TMP1
	DCA TMP1	/POINTS TO REL BLOCK TO PATCH
	CLA CLL IAC	/DEVICE 1
	CIF 10
	JMS I (7700	/CALL USR
	2		/LOOKUP
FNB,	0		/POINTER TO FILENAME, LATER BLOCK NR
L,	0		/ARG(2) WILL BE NEGATIVE LENGTH
	JMP I DPATCH	/FILE NOT FOUND
DLP,	TAD I TMP1	/GET BLOCK TO PATCH
	SPA CLA		/NEGATIVE IS END OF LIST
	JMP I DPATCH	/READY
	TAD I TMP1	/GET BLOCK OFFSET AGAIN
	TAD FNB		/RELATIVE TO THIS BLOCK
	DCA BLK
	ISZ TMP1	/POINT TO OFFSET
	JMS I (7607	/CALL SYS HNDLR
	0200		/READ ONE BLOCK
BFA,	BUF		/INTO BUF
BLK,	0		/BLOCKNUMBER
	HLT
	TAD I TMP1	/GET OFFSET IN BLOCK
	ISZ TMP1	/POINTS TO NEW VALUE
	TAD BFA
	DCA PADR	/SET ADDRESS TO PATCH
	TAD I TMP1	/GET NEW VALUE
	ISZ TMP1	/POINTS TO NEW BLOCK OFFSET
	DCA I PADR	/APPLY PATCH
	TAD BLK		/BLOCK TO WRITE
	DCA WBLK
	JMS I (7607
	4200		/WRITE BACK BLOCK
	BUF
WBLK,	0
	HLT
	JMP DLP

TMP1,	0
PADR,	0

	PAGE

BUF,	ZBLOCK 400
	$
